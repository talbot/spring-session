pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/plugins-release' }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.9"
    id "io.spring.ge.conventions" version "0.0.7"
}

rootProject.name = 'spring-session-build'

include 'spring-session-core'
include 'spring-session-data-mongodb'
include 'spring-session-data-redis'
include 'spring-session-docs'
include 'spring-session-hazelcast'
include 'spring-session-jdbc'
include 'hazelcast4'
project(':hazelcast4').projectDir = file('spring-session-hazelcast/hazelcast4')
include 'hazelcast5'
project(':hazelcast5').projectDir = file('spring-session-hazelcast/hazelcast5')

file('spring-session-samples').eachDirMatch(~/spring-session-sample-.*/) { dir ->
    include dir.name
    project(":$dir.name").projectDir = dir
}

rootProject.children.each { project ->
    project.buildFileName = "${project.name}.gradle"
}
